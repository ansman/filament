// Simple lit material that defines 3 parameters:
// - baseColor
// - roughness
// - metallic
//
// These parameters can be used by the application to change the appearance of the material.
//
// This source material must be compiled to a binary material using the matc tool.
// The command used to compile this material is:
// matc -p mobile -a opengl -o app/src/main/assets/lit.filamat app/src/materials/lit.mat
//
// See build.gradle for an example of how to compile materials automatically
// Please refer to the documentation for more information about matc and the materials system.

material {
    name : lit,

    // Dynamic lighting is enabled on this material
    shadingModel : lit,

    // We don't need to declare a "requires" array, lit materials
    // always requires the "tangents" vertex attribute (the normal
    // is required for lighting, tangent/bitangent for normal mapping
    // and anisotropy)

    // Custom vertex shader outputs
    variables : [
        uv
    ],

    // List of parameters exposed by this material
    parameters : [
        // The color must be passed in linear space, not sRGB
        {
            type : float3,
            name : baseColor
        },
        {
            type : float,
            name : roughness
        },
        {
            type : float,
            name : metallic
        },
        {
            type : samplerExternal,
            name : video
        }
    ],
}

vertex {
    void materialVertex(inout MaterialVertexInputs material) {
        material.uv = 0.5 * (getPosition() + vec4(1));
    }
}

fragment {
    void material(inout MaterialInputs material) {
        prepareMaterial(material);

        // Nothing fancy here, we simply copy the parameters
        material.baseColor.rgb = materialParams.baseColor;

        if (variable_uv.y >= 1.0) {
            material.baseColor.rgb = texture(materialParams_video, variable_uv.xz).rgb;
            material.baseColor.rg += 0.5 * variable_uv.xz;
        }

        material.roughness = materialParams.roughness;
        material.metallic = materialParams.metallic;
    }
}
